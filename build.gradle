import org.apache.tools.ant.filters.ReplaceTokens
plugins {
    id "com.github.johnrengelman.shadow" version "6.0.0"
    id "org.sonarqube" version "3.0"
    id "org.owasp.dependencycheck" version "5.3.2.1"
    id "jacoco"
}


apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'jacoco'

description = 'payment-method-sct-equens'
group = 'com.payline.payment.sct-equens'
version = '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8


compileJava.options.encoding = 'UTF-8'

ext {
    paymentMethodApiVersion = '1.9'
    paymentMethodIntegrationVersion = '1.4'
}

def externalFileName = "$projectDir/.external"
if (file(externalFileName).exists()) {
    apply from: "external.gradle"
} else {
    apply from: "monext.gradle"
}

dependencies {
    compile group:'com.payline', name: 'payment-method-api', version: "$paymentMethodApiVersion"

    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    implementation group: 'org.tomitribe', name: 'tomitribe-http-signatures', version: '1.4'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0-M1'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.7.0-M1'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

assemble.dependsOn shadowJar

shadowJar {
    archiveClassifier.set("")
    dependencies {
        exclude('com.payline:payment-method-api')
        exclude('org.apache.logging.log4j:log4j-api:2.13.3')
        exclude('org.apache.logging.log4j:log4j-core:2.13.3')

        exclude('org.junit.jupiter:junit-jupiter-params')
        exclude('org.junit.jupiter:junit-jupiter-api')

    }

}

processResources {
    // Replace tokens in release.properties file by their real value
    filesMatching('release.properties') {
        filter(ReplaceTokens, tokens: [
                'version': project.version.replaceFirst("-.*", ""),
                'date'   : new Date().format("dd/MM/yyyy")
        ])
    }
}

test {
    useJUnitPlatform()
    exclude('**/*IT.class')
}
jacocoTestReport {
    reports {
        xml.enabled true
    }
}